#!ipxe

## Configuration defaults
set coreos/stream           stable
set coreos/ignition_profile default
set coreos/install_device   /dev/sda

## Detect host architecture... badly. Can be overridden by config-menu
iseq ${buildarch} i386 && set coreos/arch x86_64 ||
  iseq ${buildarch} arm32 || iseq ${buildarch} arm64 && set coreos/arch aarch64 ||
    set coreos/arch UNKNOWN

:reload

## Reload stream variables if the configured stream is changed
iseq ${coreos/stream} ${state/loaded_stream} ||
  chain --autofree https://jmanero.github.io/ignition-config/ipxe/streams/${coreos/stream}.ipxe && 
  set ${state/loaded_stream} ${coreos/stream}

set boot/base_url     https://builds.coreos.fedoraproject.org/prod/streams/${coreos/stream}/builds/${coreos/version}/${coreos/arch}
set boot/ignition_url https://jmanero.github.io/ignition-config/ignition/${coreos/ignition_profile}.ign.json
set boot/kernel_url   ${boot/base_url}/fedora-coreos-${coreos/version}-live-kernel-${coreos/arch}
set boot/initrd_url   ${boot/base_url}/fedora-coreos-${coreos/version}-live-initramfs.${coreos/arch}.img
set boot/rootfs_url   ${boot/base_url}/fedora-coreos-${coreos/version}-live-rootfs.${coreos/arch}.img

:menu
menu Install Fedora Coreos

item live       Live Boot Fedora CoreOS ${coreos/version}-${coreos/arch}
item install    Install Fedora CoreOS ${coreos/version}-${coreos/arch}
item configure  Configure Fedora CoreOS
item shell      Start iPXE Shell

choose --default live --timeout 10000 target && goto ${target}

:live
echo Live Boot Fedora CoreOS ${coreos/version}-${coreos/arch}
prompt --key 0x03 --timeout 2000 Press Ctrl-C to return to boot menu && goto :menu ||

kernel ${boot/kernel_url} initrd=main coreos.live.rootfs_url=${boot/rootfs_url} ignition.config.url=${boot/ignition_url} ignition.firstboot ignition.platform.id=metal
initrd --name main ${boot/initrd_url}
boot

:install
echo Install Fedora CoreOS ${coreos/version}-${coreos/arch}
prompt --key 0x03 --timeout 2000 Press Ctrl-C to return to boot menu && goto :menu ||

kernel ${boot/kernel_url} initrd=main coreos.live.rootfs_url=${boot/rootfs_url} coreos.inst.install_dev=${coreos/install_device} coreos.inst.ignition_url=${boot/ignition_url} ignition.platform.id=metal
initrd --name main ${boot/initrd_url}
boot

:configure
config coreos
goto :reload

:shell
shell
